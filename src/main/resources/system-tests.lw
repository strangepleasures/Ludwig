package system-tests

def test-fields-and-properties :
	= tag 1
	assert== 1 tag
	= o Any
	= tag o 123
	assert== 123 tag o
	= r ref tag
	assert== 123 call r o

def test-assert :
	assert true 'Shouldn\'t fail'

def test-compose :
	= f compose ref exp λ x : sin x
	assert
		==
			call f 1
			exp sin 1
		'Function composition'

def test-range :
	= r range 0 10
	assert== 10 size r
	assert== 0 min r
	assert== 9 max r

def test-list :
	= a list 1 2 3
	assert== 3 size a
	assert== 1 get a 0
	assert== 2 get a 1
	assert== 3 get a 2

def test-map :
	= src list 1 2 3
	= dst to-list map src λ x : * x x
	assert==
		list 1 4 9
		dst

def test-lazy-sequences :
	= result null
	= n 1000000
	= seq map range 0 + n 1 λ x : x
	for i seq
		= result i
	assert== result n

def test-refs :
	= _exp ref exp
	assert== exp 1 call _exp 1
	= _fact ref fact
	assert== fact 5 call _fact 5

field tag

class Point2D Any x y

class Point3D Point2D z

def test-oop :
	= p2 Point2D 1 2
	assert== 2 dim p2
	= p3 Point3D 1 2 3
	assert== 3 dim p3

def dim o :
	0

method Point2D dim o :
	2

method Point3D dim o :
	3
