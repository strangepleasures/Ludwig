package system-tests

def test-assert :
	assert true "Shouldn't fail"

def test-compose :
	= f compose ref exp λ x : sin x
	assert
		==
			call f 1
			exp sin 1
		"Function composition"

def test-range :
	= r range 0 10
	assert== 10 size r
	assert== 0 min r
	assert== 9 max r

def test-list :
	= a list 1 2 3
	assert== 3 size a
	assert== 1 at a 0
	assert== 2 at a 1
	assert== 3 at a 2

def test-map :
	= src list 1 2 3
	= dst to-list map src λ x : * x x
	assert==
		list 1 4 9
		dst

def test-lazy-sequences :
	= result null
	= n 1000000
	= seq map range 0 + n 1 λ x : x
	for i seq
		= result i
	assert== result n

def test-fields-and-properties :
	= x field
	= y field
	= r property
		\ it :
			+ get x it get y it
		null
	= o Object
	set x o 3
	set y o 4
	assert== 3 get x o
	assert== 4 get y o
	assert== 7 get r o